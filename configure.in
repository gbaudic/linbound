#Generated 2008-07-30 by G. B. then completed with a lot of stuff from wormux configure.ac
dnl *** NOT TESTED YET !!! *********

AC_INIT(main.cpp)
AM_INIT_AUTOMAKE(linbound,0.1)

dnl *** Check for gettext and friends ***************************************

AM_GNU_GETTEXT([external])

if test "x$USE_NLS" != "xyes"; then
  AC_MSG_WARN([gettext is disabled. If you want internationalisation, please install gettext package.])
fi

dnl **************************
dnl *** Usual tools needed ***
dnl **************************
AC_PROG_CC
AC_PROG_CXX
AC_PROG_CPP
AC_PROG_INSTALL

AC_CHECK_PROG(cxx_present, $CXX, "yes", "no")
if test "x$cxx_present" != "xyes"; then
  AC_MSG_ERROR([*** No C++ compiler can be found!])
fi

dnl *****************
dnl *** SDL check ***
dnl *****************

SDL_VERSION=1.2.6

AM_PATH_SDL($SDL_VERSION,
            :,
            AC_MSG_ERROR([*** SDL version $SDL_VERSION not found!]))
CXXFLAGS="$CXXFLAGS $SDL_CFLAGS"
CFLAGS="$CFLAGS $SDL_CFLAGS"
CPPFLAGS="$CPPFLAGS $SDL_CFLAGS"
LIBS="$LIBS $SDL_LIBS"

dnl *** png check, used by SDL_image ... is it really compulsory since sdl_image requests libpng as a dependency ???***

dnl *********************
dnl *** SDL_gfx check ***
dnl *********************

AC_ARG_WITH(sdl-gfx-prefix,
            [  --with-sdl-gfx-prefix=DIR       specify where SDL_gfx library is installed],
            [SDL_GFX_PREFIX="$withval"])
AC_SUBST(SDL_GFX_PREFIX)
if test "x$SDL_GFX_PREFIX" != "x"; then
  CPPFLAGS="$CPPFLAGS -I$SDL_GFX_PREFIX/include/SDL"
  CXXFLAGS="$CXXFLAGS -I$SDL_GFX_PREFIX/include/SDL"
  LIBS="-L$SDL_GFX_PREFIX/lib $LIBS"
fi
if test "$static" = yes; then
  LIBS="$LIBS -lm -ldl -lfreetype -lz"
fi
AC_CHECK_LIB(SDL_gfx, rotozoomSurfaceXY,,check_sdl_gfx="no")
AC_CHECK_HEADERS(SDL_rotozoom.h,,check_sdl_gfx="no")

dnl *********************
dnl *** libcURL check ***
dnl *********************

LIBCURL_CHECK_CONFIG([yes],[7.0],[check_curl=yes],[check_curl=no])

dnl ****** Check for pkg-config to check for libxml2... **************************

AC_PATH_PROGS([PACKAGE_CONFIG], [$host_alias-pkg-config pkg-config], [none])
if test "x$PACKAGE_CONFIG" = "xnone"; then
  AC_MSG_ERROR([*** pkg-config, needed to check for libxml2 existence has not been found.])
fi

dnl ****** Check for the whole SDL shabang and libxml2 ***************************

AC_CHECK_LIB($PNG, png_error,,,)
AC_CHECK_LIB(SDL_image, IMG_Load,,check_sdl_image="no")
AC_CHECK_LIB(SDL_mixer, Mix_OpenAudio,,check_sdl_mixer="no")
AC_CHECK_LIB([SDL_ttf], [TTF_OpenFont],,check_sdl_ttf="no")
AC_CHECK_LIB([SDL_net], [SDLNet_Init],,check_sdl_net="no")
if test "${WIN32}" = "yes"; then LIBS="$LIBS -lwsock32 -lws2_32 -luser32"; fi

AC_MSG_CHECKING(for libxml2)

if $PACKAGE_CONFIG libxml-2.0 --libs > /dev/null 2>&1; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  check_libxml="no"
fi

dnl *** Errors to display when missing ***********************************

if test \
     x${check_sdl_image} = xno \
  -o x${check_sdl_mixer} = xno \
  -o x${check_sdl_ttf} = xno \
  -o x${check_sdl_gfx} = xno \
  -o x${check_sdl_net} = xno \
  -o x${check_libxml} = xno \
  -o x${check_curl} = xno \
  ; then
  AC_MSG_RESULT([])
  if test x${check_sdl_image} = xno; then
    AC_MSG_RESULT([ERROR: SDL_image library required])
  fi
  if test x${check_sdl_mixer} = xno; then
    AC_MSG_RESULT([ERROR: SDL_mixer library required])
  fi
  if test x${check_sdl_ttf} = xno; then
    AC_MSG_RESULT([ERROR: SDL_ttf library required])
  fi
  if test x${check_sdl_gfx} = xno; then
    AC_MSG_RESULT([ERROR: SDL_gfx library version >= 2.0.13 required])
  fi
  if test x${check_sdl_net} = xno; then
    AC_MSG_RESULT([ERROR: SDL_net library required])
  fi
  if test x${check_libxml} = xno; then
    AC_MSG_RESULT([ERROR: libxml2 not found!])
  fi
  if test x${check_curl} = xno; then
    AC_MSG_RESULT([ERROR: libcURL not found!])
  fi
  exit 1
fi


AC_OUTPUT(Makefile ./po/Makefile.in)
